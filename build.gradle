buildscript {
    repositories {
        google()
        mavenCentral()
    }
}
plugins {
    id 'java'
    id 'maven-publish'
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

group = project_group
version = project_version

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.23.1'
    implementation group: 'com.google.guava', name: 'guava', version: '33.3.0-jre'
    implementation group: 'org.realityforge.org.jetbrains.annotations', name: 'org.jetbrains.annotations', version: '1.7.0'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.16'
}

jar {
    manifest.attributes([
            "FMLModType": "GAMELIBRARY"
    ])
}
sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources {
            srcDirs 'src/main/resources'
        }
        runtimeClasspath += compileClasspath
    }
}

// 打包sourcesJar任务
tasks.register('sourcesJar', Jar) {
    dependsOn classes

    from sourceSets.main.allSource
}

// 打包javadocJar任务
tasks.register('javadocJar', Jar) {
    dependsOn javadoc

    from javadoc.destinationDir
}

// 解决javadoc打包乱码
javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        title "DG_LAB"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            name "xqsw"
            url "https://maven.xiaobuawa.top/"
            credentials {
                 username project.findProperty("MAVEN_REPO_USER") ?: "" // 仓库发布用户名
                 password project.findProperty("MAVEN_REPO_PASS") ?: "" // 仓库发布用户密码
             }
        }
    }
}
